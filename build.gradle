plugins {
    id 'java'
    id 'maven'

    id 'checkstyle'
    id 'findbugs'

    id 'eclipse'
    id 'idea'
}


group = 'org.jlab.coda'
archivesBaseName = 'jclara'
version = '4.3-SNAPSHOT'

defaultTasks 'build'

compileJava {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
}

configurations {
    all {
         resolutionStrategy.cacheChangingModulesFor 30, 'minutes'
    }
}

sourceSets {
    main {
        java {
            srcDir 'src'
        }
    }
    test {
        java {
            srcDir 'test'
        }
        resources {
            srcDir 'test'
            include "resources/**"
        }
    }
}


repositories {
    maven {
        url 'https://clasweb.jlab.org/clas12maven/'
    }
    mavenCentral()
}


dependencies {
    compile 'org.jlab.coda:xmsg:2.3'
    compile 'org.yaml:snakeyaml:1.18'
    compile 'org.json:json:20160810'
    compile 'org.jline:jline:3.1.3'
    compile 'org.apache.commons:commons-exec:1.3'
    compile 'org.freemarker:freemarker:2.3.26-incubating'
    compile 'net.sf.jopt-simple:jopt-simple:4.9'
    compile 'com.google.code.findbugs:jsr305:3.0.1'
    compile 'org.jlab.coda:jinflux:1.0-SNAPSHOT'
    testCompile 'junit:junit:4.12'
    testCompile 'org.hamcrest:hamcrest-library:1.3'
    testCompile 'org.mockito:mockito-core:1.10.19'
}


javadoc {
    options.charSet = 'utf8'
    options.encoding = 'utf8'
    options.docEncoding = 'utf8'

    include "org/jlab/clara/base/**"
    include "org/jlab/clara/engine/**"
    include "org/jlab/clara/std/**"
    include "org/jlab/clara/sys/**"
    exclude "org/jlab/clara/base/core/**"
    exclude "org/jlab/clara/sys/ccc/**"
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}


task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}


artifacts {
    archives javadocJar
    archives sourcesJar
}


if (!hasProperty('localPublicationRepo')) {
    ext.localPublicationRepo = 'file://localhost/tmp/repo'
}

uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: localPublicationRepo)
        }
    }
}


test {
    testLogging {
        exceptionFormat = 'full'
    }
    useJUnit {
        excludeCategories 'org.jlab.clara.IntegrationTest'
    }
}

task integrationTest(type: Test) {
    useJUnit {
        includeCategories 'org.jlab.clara.IntegrationTest'
    }
    testLogging {
        exceptionFormat = 'full'
    }
    outputs.upToDateWhen { false }
}


def deploySpec = copySpec {
    into ('lib') {
        from configurations.runtime
        from jar.archivePath
    }

    from ('scripts/unix') {
        into 'bin'
        exclude 'etc'
        fileMode 0755
    }

    from ('scripts/lib') {
        into 'lib/clara'
        fileMode 0755
    }

    // trick Gradle to "create" empy directories
    def tempDir = "${buildDir}/distributions/clara_home"

    file("${tempDir}/log").mkdirs()
    file("${tempDir}/plugins").mkdirs()

    from tempDir
}


task distSources(type: Tar, dependsOn: build) {
    extension = 'tar.gz'
    classifier = 'src'
    compression = Compression.GZIP

    into "${baseName}-${version}-${classifier}"

    from 'README.md'
    from 'build.gradle'
    from 'gradlew'
    from 'gradlew.bat'

    from ('config') {
        into 'config'
    }
    from ('gradle') {
        into 'gradle'
    }
    from ('scripts') {
        into 'scripts'
    }
    from ('src') {
        into 'src'
    }
    from ('test') {
        into 'test'
    }
}


task distBinaries(type: Tar, dependsOn: build) {
    extension = 'tar.gz'
    compression = Compression.GZIP

    into "${baseName}-${version}"
    with deploySpec
}


task deploy(type: Copy, overwrite: true, dependsOn: jar) {
    def dest = "$System.env.CLARA_HOME"

    into dest
    with deploySpec

    doFirst {
        if (dest == 'null') {
            throw new GradleException('CLARA_HOME not set')
        }
    }
}



ext.classPathCache = file("${buildDir}/tmp/classpath")
ext.testClassPathCache = file("${buildDir}/tmp/test_classpath")

task cacheClasspath {
    doLast {
        if (!classPathCache.isFile()) {
            classPathCache.parentFile.mkdirs()
            classPathCache.createNewFile()
        }
        classPathCache.write sourceSets.main.runtimeClasspath.asPath
        testClassPathCache.write sourceSets.test.runtimeClasspath.asPath
    }
}
cacheClasspath.inputs.files sourceSets.main.runtimeClasspath
cacheClasspath.inputs.files sourceSets.test.runtimeClasspath
cacheClasspath.outputs.files classPathCache
cacheClasspath.outputs.files testClassPathCache

task printClasspath {
    doLast {
        println classPathCache.text
    }
}

build.dependsOn cacheClasspath
printClasspath.dependsOn cacheClasspath


def ciMode() {
    if (hasProperty("ciMode")) {
        return ciMode.toBoolean()
    }
    return false
}


checkstyle {
    toolVersion = '8.0'
    configFile = file('config/quality/checkstyle.xml')
    configProperties['samedir'] = file('config/quality')
}

findbugs {
    toolVersion = '3.0.1'
    ignoreFailures = true
    effort = 'max'
    reportLevel = 'medium'
    excludeFilter = file('config/quality/findbugs-exclude.xml')
}

tasks.withType(FindBugs) {
    def useXml = ciMode()
    reports {
        xml.enabled = useXml
        html.enabled = !xml.enabled
    }
}

// Marker task to enable findbugs.
task findbugs(
    group: 'Verification',
    description: 'Marker task to enabled findbugs.'
)

task checkFindBugsResults {
    doLast {
        def bugsFound = 0
        [findbugsMain, findbugsTest].forEach {
            bugsFound += printFindBugs it.reports.xml.destination
        }
        if (bugsFound > 0) {
            throw new GradleException("$bugsFound FindBugs rule violations were found.")
        }
    }
}

def printFindBugs(File xml) {
    def slurped = new XmlSlurper().parse(xml)
    def bugs = slurped.BugInstance

    bugs.each { bug ->
        def line = bug.SourceLine
        logger.error "[FindBugs] ${line.@sourcepath}:${line.@start}:${line.@end} [${bug.@type}]"
    }
    bugs.size()
}

if (ciMode()) {
    checkFindBugsResults.mustRunAfter findbugsMain, findbugsTest
    check.dependsOn checkFindBugsResults
}

gradle.taskGraph.whenReady { taskGraph ->
    tasks.findbugsMain.onlyIf {
        taskGraph.hasTask((tasks.findbugs))
    }
    tasks.findbugsTest.onlyIf {
        taskGraph.hasTask((tasks.findbugs))
    }
    tasks.checkFindBugsResults.onlyIf {
        taskGraph.hasTask((tasks.findbugs))
    }
}



eclipse {
    classpath {
        defaultOutputDir = file("${buildDir}/eclipse-classes")
    }
}
