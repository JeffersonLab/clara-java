apply plugin: 'java'
apply plugin: 'maven'

apply plugin: "findbugs"

apply plugin: 'eclipse'
apply plugin: 'idea'


archivesBaseName = 'jclara'
group = 'org.jlab.coda'
version = '4.3-SNAPSHOT'

defaultTasks 'build'

compileJava {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
}


configurations {
    provided {
        transitive = false
    }
}

sourceSets {
    main {
        java {
            srcDir 'src'
        }
        compileClasspath += configurations.provided
    }
    test {
        java {
            srcDir 'test'
        }
        compileClasspath += configurations.provided
    }
}


repositories {
    mavenLocal()
    maven {
        url 'https://userweb.jlab.org/~smancill/repo/'
    }
    mavenCentral()
}


dependencies {
    compile 'org.jlab.coda:xmsg:2.3-SNAPSHOT'
    compile 'net.sf.jopt-simple:jopt-simple:4.9'
    compile 'org.yaml:snakeyaml:1.16'
    compile ('com.googlecode.json-simple:json-simple:1.1.1') {
        exclude group: "junit", module: "*"
    }
    compile 'com.google.code.findbugs:jsr305:3.0.0'
    compile 'org.apache.jena:apache-jena-libs:3.0.1'
    provided 'edu.stanford.protege:org.protege.editor.owl.codegeneration:1.0.2'
    provided 'net.sourceforge.owlapi:owlapi-distribution:3.4.10'
    testCompile 'junit:junit:4.+'
    testCompile 'org.hamcrest:hamcrest-library:1.3'
    testCompile "org.mockito:mockito-core:1.+"
}



javadoc {
    classpath += configurations.provided
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

if (JavaVersion.current().isJava8Compatible()) {
    allprojects {
        tasks.withType(Javadoc) {
            options.addStringOption('Xdoclint:none', '-quiet')
        }
    }
}


task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}


artifacts {
    archives javadocJar
    archives sourcesJar
}



task(startDpe, dependsOn: 'classes', type: JavaExec) {
    main = 'org.jlab.clara.sys.Dpe'
    classpath = sourceSets.main.runtimeClasspath
    if ( project.hasProperty("dpeArgs") ) {
        args Eval.me(dpeArgs)
    }
    errorOutput = System.out
}


task(orInteractive, dependsOn: 'classes', type: JavaExec) {
    main = 'org.jlab.clara.examples.orchestrators.OrInteractive'
    classpath = sourceSets.main.runtimeClasspath
    if ( project.hasProperty("orArgs") ) {
        args Eval.me(orArgs)
    }
    standardInput = System.in
    errorOutput = System.out
}



def deploySpec = copySpec {
    into ('lib') {
        from configurations.runtime
        from "${jar.archivePath}"
    }

    from ("${project.rootDir}/scripts/unix") {
        into 'bin'
        fileMode 0755
    }
}


task distSources(type: Tar, dependsOn: build) {
    extension = 'tar.gz'
    classifier = 'src'
    compression = Compression.GZIP

    into "${baseName}-${version}-${classifier}"
    from "${project.rootDir}/build.gradle"
    from "${project.rootDir}/build.xml"
    from ("${project.rootDir}/src") {
        into 'src'
    }
    from ("${project.rootDir}/scripts") {
        into 'scripts'
    }
}


task distBinaries(type: Tar, dependsOn: build) {
    extension = 'tar.gz'
    compression = Compression.GZIP

    into "${baseName}-${version}"
    with deploySpec
}


task deploy(type: Copy, overwrite: true, dependsOn: jar) {
    def dest = "$System.env.CLARA_HOME"
    def dir = new File(dest)
    into dir
    with deploySpec

    doFirst {
        if (dest == "null") {
            throw new GradleException('CLARA_HOME not set')
        }
        dir.mkdirs()
    }
}



findbugs {
    toolVersion = "3.0.1"
    ignoreFailures = true
    effort = "default"
    reportLevel = "medium"
    excludeFilter = new File(rootDir, "config/quality/findbugs-exclude.xml")
}

tasks.withType(FindBugs) {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}

/*
 *   Copyright (c) 2016.  Jefferson Lab (JLab). All rights reserved. Permission
 *   to use, copy, modify, and distribute  this software and its documentation for
 *   educational, research, and not-for-profit purposes, without fee and without a
 *   signed licensing agreement.
 *
 *   IN NO EVENT SHALL JLAB BE LIABLE TO ANY PARTY FOR DIRECT, INDIRECT, SPECIAL
 *   INCIDENTAL, OR CONSEQUENTIAL DAMAGES, INCLUDING LOST PROFITS, ARISING
 *   OUT OF THE USE OF THIS SOFTWARE AND ITS DOCUMENTATION, EVEN IF JLAB HAS
 *   BEEN ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
 *
 *   JLAB SPECIFICALLY DISCLAIMS ANY WARRANTIES, INCLUDING, BUT NOT LIMITED TO,
 *   THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR
 *   PURPOSE. THE CLARA SOFTWARE AND ACCOMPANYING DOCUMENTATION, IF ANY,
 *   PROVIDED HEREUNDER IS PROVIDED "AS IS". JLAB HAS NO OBLIGATION TO PROVIDE
 *   MAINTENANCE, SUPPORT, UPDATES, ENHANCEMENTS, OR MODIFICATIONS.
 *
 *   This software was developed under the United States Government license.
 *   For more information contact author at gurjyan@jlab.org
 *   Department of Experimental Nuclear Physics, Jefferson Lab.
 */

// Marker task to enable findbugs.
task findbugs(
    group: "Verification",
    description: "Marker task to enabled findbugs."
)

gradle.taskGraph.whenReady { taskGraph ->
    tasks.findbugsMain.onlyIf {
        taskGraph.hasTask((tasks.findbugs))
    }
    tasks.findbugsTest.onlyIf {
        taskGraph.hasTask((tasks.findbugs))
    }
}



eclipse {
    classpath {
        defaultOutputDir = file("${buildDir}/eclipse-classes")
        plusConfigurations += [ configurations.provided ]
    }
}

idea {
  module{
    scopes.PROVIDED.plus += [ configurations.provided ]
  }
}
