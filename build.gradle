apply plugin: 'java'
apply plugin: 'maven'

apply plugin: 'eclipse'
apply plugin: 'idea'


archivesBaseName = 'jclara'
group = 'org.jlab.coda'
version = '1.2-SNAPSHOT'

defaultTasks 'build'


sourceSets {
    main {
        java {
            srcDir 'src'
        }
    }
    test {
        java {
            srcDir 'test'
        }
    }
}


repositories {
    mavenLocal()
    maven {
        url 'https://userweb.jlab.org/~smancill/repo/'
    }
    mavenCentral()
}


dependencies {
    compile 'org.jlab.coda:xmsg:1.0.2-SNAPSHOT'
}



task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}


task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}


artifacts {
    archives javadocJar
    archives sourcesJar
}



task(startDpe, dependsOn: 'classes', type: JavaExec) {
    main = 'org.jlab.clara.sys.Dpe'
    classpath = sourceSets.main.runtimeClasspath
}



def deploySpec = copySpec {
    into ('lib') {
        from configurations.runtime
        from "${jar.archivePath}"
    }

    from ("${project.rootDir}/scripts/unix") {
        into 'bin'
        fileMode 0755
    }
}


task distSources(type: Tar, dependsOn: build) {
    extension = 'tar.gz'
    classifier = 'src'
    compression = Compression.GZIP

    into "${baseName}-${version}-${classifier}"
    from "${project.rootDir}/build.gradle"
    from "${project.rootDir}/build.xml"
    from ("${project.rootDir}/src") {
        into 'src'
    }
    from ("${project.rootDir}/scripts") {
        into 'scripts'
    }
}


task distBinaries(type: Tar, dependsOn: build) {
    extension = 'tar.gz'
    compression = Compression.GZIP

    into "${baseName}-${version}"
    with deploySpec
}


task deploy(type: Copy, overwrite: true, dependsOn: jar) {
    def dest = "$System.env.CLARA_HOME"
    def dir = new File(dest)
    into dir
    with deploySpec

    doFirst {
        if (dest == "null") {
            throw new GradleException('CLARA_HOME not set')
        }
        dir.mkdirs()
    }
}



eclipse {
    classpath {
        defaultOutputDir = file("${buildDir}/eclipse-classes")
    }
}
